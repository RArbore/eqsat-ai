use core::str::FromStr;

use crate::database::Database;
use crate::frontend::*;

grammar(interner: &mut Interner, database: &mut Database);

pub Program: Vec<Rule> = {
    => Vec::new(),
    <mut p:Program> <x:Rule> ";" => {
        p.push(x);
        p
    },
    <p:Program> <x:Declaration> ";" => {
        database.register_table(x.0, x.1, x.2);
        p
    },
}

Declaration: (Symbol, usize, usize) = {
    ".decl(" <s:Symbol> <det:Usize> <dep:Usize> ")" => (s, det, dep),
}

Rule: Rule = {
    <query:Query> "=>" <action:Action> => {
        Rule { query, action }
    }
}

Query: Query = {
    <atoms:AtomList> => {
        Query { atoms }
    }
}

Action: Action = {
    <atoms:AtomList> => {
        Action::InsertPattern{ atoms }
    }
}

AtomList: Vec<Atom> = {
    => Vec::new(),
    <mut l:AtomList> <a:Atom> => {
        l.push(a);
        l
    }
}

Atom: Atom = {
    <s:Symbol> "(" <slots:SlotList> ")" => Atom { table: database.table_id(s), slots },
}

SlotList: Vec<Slot> = {
    => Vec::new(),
    <mut l:SlotList> <a:Slot> => {
        l.push(a);
        l
    }
}

Slot: Slot = {
    "_" => Slot::Wildcard,
    U32 => Slot::Concrete(<>),
    Symbol => Slot::Variable(<>),
}

Symbol: Symbol = {
    Iden => interner.get_or_intern(<>),
}

Usize: usize = r"-?[0-9]+" => usize::from_str(<>).unwrap();
U32: u32 = r"-?[0-9]+" => u32::from_str(<>).unwrap();
Iden: &'input str = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>;
